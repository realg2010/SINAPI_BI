SUBROUTINE JSL.TOPS.XTR.FTL

*============================================
* Title: BI Funds Transfer Data Extraction
* Initial Author: Yisau Ramon ; For Tops Analytics
* Date: 1st May 2018
*============================================

$INSERT I_COMMON
$INSERT I_EQUATE
$INSERT I_F.FUNDS.TRANSFER
$INSERT I_F.DATES
$INSERT I_F.COMPANY
$INSERT I_F.INTERCO.PARAMETER

GOSUB INIT
GOSUB PROCESS
RETURN

INIT:
	FN.INTERCO='F.INTERCO.PARAMETER'
	F.INTERCO=''
	CALL OPF(FN.INTERCO,F.INTERCO)
	LWORK.DATE=R.DATES(EB.DAT.LAST.WORKING.DAY) ; CR.BRANCH.NAME='' ; DR.BRANCH.NAME='' ; BRANCH.NAME=''
	DB.DATE=TODAY[1,4]:'-':TODAY[5,2]:'-':TODAY[7,2] ; OUT.ARR='' ; R.FT='' ; PY.LIST='' ; DLM='|'
	REPORT.DATE=R.DATES(EB.DAT.LAST.WORKING.DAY)[1,4]:'-':R.DATES(EB.DAT.LAST.WORKING.DAY)[5,2]:'-':R.DATES(EB.DAT.LAST.WORKING.DAY)[7,2]
	SYS.ID='SYSTEM' ; API.ID='JSL.TOPS.API' ; V.RESPONSE='' ; RET.ERR=''
	READ R.INTERCO FROM F.INTERCO,SYS.ID ELSE NULL
	MNE.LIST=R.INTERCO<ST.ICP.FIN.MNEMONIC>
RETURN

PROCESS:
     MNE.CNT=DCOUNT(MNE.LIST,@VM)
     FOR J=1 TO MNE.CNT
	 MNE=MNE.LIST<1,J>
     FN.FT='F':MNE:'.FUNDS.TRANSFER'
	 F.FT=''
	 CALL OPF(FN.FT,F.FT)
     FT.CMD='SELECT ':FN.FT:' WITH PROCESSING.DATE EQ ':LWORK.DATE
	 CALL EB.READLIST(FT.CMD,FT.LIST,'','','')
	 LOOP
	 REMOVE FT.ID FROM FT.LIST SETTING FT.POS
	     IF FT.POS=0 THEN GOSUB LOAD.DATA
	 WHILE FT.ID:FT.POS
	 CALL F.READ(FN.FT,FT.ID,R.FT,F.FT,FT.ERR)
	 CALL DBR('COMPANY':FM:EB.COM.COMPANY.NAME,R.FT<FT.CREDIT.COMP.CODE>,CR.BRANCH.NAME)
	 CALL DBR('COMPANY':FM:EB.COM.COMPANY.NAME,R.FT<FT.DEBIT.COMP.CODE>,DR.BRANCH.NAME)
	 CALL DBR('COMPANY':FM:EB.COM.COMPANY.NAME,R.FT<FT.CO.CODE>,BRANCH.NAME)
	 CHANGE ',' TO ' ' IN CR.BRANCH.NAME
	 CHANGE ',' TO ' ' IN DR.BRANCH.NAME
	 CHANGE ',' TO ' ' IN BRANCH.NAME
	 DR.DATE=R.FT<FT.DEBIT.VALUE.DATE>[1,4]:'-':R.FT<FT.DEBIT.VALUE.DATE>[5,2]:'-':R.FT<FT.DEBIT.VALUE.DATE>[7,2]
	 CR.DATE=R.FT<FT.CREDIT.VALUE.DATE>[1,4]:'-':R.FT<FT.CREDIT.VALUE.DATE>[5,2]:'-':R.FT<FT.CREDIT.VALUE.DATE>[7,2]
	 PROCESS.DATE=R.FT<FT.PROCESSING.DATE>[1,4]:'-':R.FT<FT.PROCESSING.DATE>[5,2]:'-':R.FT<FT.PROCESSING.DATE>[7,2]
	 IF R.FT<FT.EXPOSURE.DATE> THEN
         EX.DATE=R.FT<FT.EXPOSURE.DATE>[1,4]:'-':R.FT<FT.EXPOSURE.DATE>[5,2]:'-':R.FT<FT.EXPOSURE.DATE>[7,2]
         END ELSE
         EX.DATE=REPORT.DATE
         END

	 OUT.ARR=FT.ID:DLM:R.FT<FT.TRANSACTION.TYPE>:DLM:R.FT<FT.DEBIT.ACCT.NO>:DLM:R.FT<FT.DEBIT.AMOUNT>:DLM:R.FT<FT.DEBIT.CURRENCY>
	 OUT.ARR:=DLM:DR.DATE:DLM:DQUOTE(R.FT<FT.DEBIT.THEIR.REF>):DLM:DQUOTE(R.FT<FT.CREDIT.THEIR.REF>):DLM:R.FT<FT.CREDIT.ACCT.NO>
	 OUT.ARR:=DLM:R.FT<FT.CREDIT.CURRENCY>:DLM:R.FT<FT.CREDIT.AMOUNT>:DLM:CR.DATE:DLM:PROCESS.DATE
	 OUT.ARR:=DLM:DQUOTE(R.FT<FT.ORDERING.CUST>):DLM:DQUOTE(R.FT<FT.ORDERING.BANK>):DLM:R.FT<FT.ACCT.WITH.BANK>:DLM:R.FT<FT.BEN.ACCT.NO>
	 OUT.ARR:=DLM:R.FT<FT.BEN.CUSTOMER>:DLM:R.FT<FT.BEN.BANK>:DLM:R.FT<FT.CHEQUE.NUMBER>:DLM:DQUOTE(R.FT<FT.PAYMENT.DETAILS>)
	 OUT.ARR:=DLM:R.FT<FT.PROFIT.CENTRE.CUST>:DLM:R.FT<FT.PROFIT.CENTRE.DEPT>:DLM:EX.DATE
	 OUT.ARR:=DLM:R.FT<FT.CREDIT.COMP.CODE>:DLM:DQUOTE(CR.BRANCH.NAME)
	 OUT.ARR:=DLM:R.FT<FT.DEBIT.COMP.CODE>:DLM:DQUOTE(DR.BRANCH.NAME):DLM:R.FT<FT.CO.CODE>:DLM:DQUOTE(BRANCH.NAME)
         OUT.ARR:=DLM:R.FT<FT.LOC.AMT.DEBITED>:DLM:R.FT<FT.LOC.AMT.CREDITED>:DLM:R.FT<FT.LOC.POS.CHGS.AMT>:DLM:R.FT<FT.LOCAL.CHARGE.AMT>:DLM:REPORT.DATE:DLM:DB.DATE
	 
	 PY.LIST<-1>=OUT.ARR
        CNT=DCOUNT(PY.LIST,@FM)
        IF CNT EQ '20000' THEN
            GOSUB LOAD.DATA
        END
        FT.ID='' ; R.FT=''; OUT.ARR='' ; CR.BRANCH.NAME='' ; DR.BRANCH.NAME='' ; BRANCH.NAME=''
		DR.DATE='' ; CR.DATE='' ; PROCESS.DATE='' ; EX.DATE='' 
	 REPEAT
         NEXT J
RETURN
LOAD.DATA:
     IF PY.LIST NE '' THEN
     PY.LIST=TRIM(PY.LIST,',','A')
     PY.LIST=TRIM(PY.LIST,"'",'A')
     PY.LIST=TRIM(PY.LIST,"~",'A')
     CHANGE FM TO ',' IN PY.LIST
     REQUEST=PY.LIST:"~FTL"
     CALL EB.CALL.JAVA.API(API.ID,REQUEST,V.RESPONSE,RET.ERR)
     PY.LIST=''
     END
    RETURN
END