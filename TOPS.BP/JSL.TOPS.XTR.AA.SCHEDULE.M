SUBROUTINE JSL.TOPS.XTR.AA.SCHEDULE
$INSERT I_COMMON
$INSERT I_EQUATE
$INSERT I_F.AA.ARRANGEMENT
$INSERT I_F.DATES
$INSERT I_F.CUSTOMER
$INSERT I_F.ACCOUNT

GOSUB INIT
GOSUB PROCESS
RETURN

INIT:
    FN.ARR='F.AA.ARRANGEMENT'
    F.ARR=''
    CALL OPF(FN.ARR,F.ARR)
    DUE.DATES = ''  ;* Holds the list of Schedule due dates
    DUE.TYPES = ''  ;* Holds the list of Payment Types for the above dates
    DUE.TYPE.AMTS = ''        ;* Holds the Payment Type amounts
    DUE.PROPS = ''  ;* Holds the Properties due for the above type
    DUE.PROP.AMTS = ''        ;* Holds the Property Amounts for the Properties above
    DUE.OUTS = ''   ;* Oustanding Bal for the date
    DUE.METHODS = ""
    DLM='|' ; SCHED.ARR = '' ; ARR.ID = '' ; DATE.REQD = '' ; CYCLE.DATE = '' ; SIM.REF = ''
	TOT.DUE.PAYM = 0 ; TOT.CAP.PAYM = 0 ; TOT.INT.PAYM = 0 ; TOT.PRIN.PAYM = 0 ; TOT.CHG.PAYM = 0 ; PROP.CLS.LIST = ''
	DB.DATE=TODAY[1,4]:'-':TODAY[5,2]:'-':TODAY[7,2] ; OUT.ARR='' ; R.ARR='' ; PY.LIST='' ; SNAME='' ; LNAME='' ; PROD.NAME=''
	REPORT.DATE=R.DATES(EB.DAT.LAST.WORKING.DAY)[1,4]:'-':R.DATES(EB.DAT.LAST.WORKING.DAY)[5,2]:'-':R.DATES(EB.DAT.LAST.WORKING.DAY)[7,2]
RETURN

PROCESS:
     ARR.CMD='SELECT ':FN.ARR:' WITH ARR.STATUS EQ CURRENT'
	 CALL EB.READLIST(ARR.CMD,ARR.LIST,'','','')
	 LOOP
	 REMOVE ARR.ID FROM ARR.LIST SETTING ARR.POS
	      IF ARR.POS EQ 0 THEN GOSUB LOAD.DATA
	 WHILE ARR.ID:ARR.POS
	 CALL F.READ(FN.ARR,ARR.ID,R.ARR,F.ARR,ARR.ERR)
	 
	 CALL DBR('CUSTOMER':FM:EB.CUS.SHORT.NAME,R.ARR<AA.ARR.CUSTOMER>,SNAME)
	 CALL DBR('CUSTOMER':FM:EB.CUS.NAME.1,R.ARR<AA.ARR.CUSTOMER>,LNAME)
	 CALL DBR('ACCOUNT':FM:AC.ACCOUNT.TITLE.1,R.ARR<AA.ARR.LINKED.APPL.ID>,PROD.NAME)
     CALL AA.SCHEDULE.PROJECTOR(ARR.ID, SIM.REF, "",CYCLE.DATE, TOT.PAYMENT, DUE.DATES, DUE.TYPES, DUE.METHODS, DUE.TYPE.AMTS, DUE.PROPS, DUE.PROP.AMTS, DUE.OUTS)     ;* Routine to Project complete schedules
	 TOT.DTES = DCOUNT(DUE.DATES,FM)     ;* Total Number of Schedule dates
     FOR DCNT = 1 TO TOT.DTES
        DUE.DATE = DUE.DATES<DCNT>      ;* Pick each date
        TOT.PAY.TYPE = DCOUNT(DUE.TYPES<DCNT>,VM)
        FOR PAY.CNT = 1 TO TOT.PAY.TYPE
           GOSUB PROCESS.PAY.TYPE
        NEXT PAY.CNT
        TOT.PAYM = TOT.PAYMENT<DCNT>    ;* Total Payment for this date
        CURRENT.OS = DUE.OUTS<DCNT>     ;* O/S on this date
		DUE.DATE=DUE.DATE[1,4]:'-':DUE.DATE[5,2]:'-':DUE.DATE[7,2]
        OUT.ARR = ARR.ID:DLM:R.ARR<AA.ARR.CUSTOMER>:DLM:SNAME:DLM:LNAME:DLM:R.ARR<AA.ARR.LINKED.APPL.ID>:DLM:PROD.NAME:DLM:DUE.DATE:DLM:TOT.DUE.PAYM
		OUT.ARR:=DLM:TOT.CAP.PAYM:DLM:TOT.PRIN.PAYM:DLM:TOT.INT.PAYM:DLM:TOT.CHG.PAYM:DLM:CURRENT.OS:DLM:REPORT.DATE:DLM:DB.DATE       ;* Build the Array for this date ;*EN_10003652 -S/E
		PY.LIST<-1>=OUT.ARR
        TOT.DUE.PAYM = 0 ; TOT.CAP.PAYM = 0 ; TOT.INT.PAYM = 0 ; TOT.PRIN.PAYM = 0 ; TOT.CHG.PAYM = 0 ; PROP.CLS.LIST = '' ; OUT.ARR=''
		
     NEXT DCNT
     CNT=DCOUNT(PY.LIST,FM)
	 IF CNT >= '20000' THEN
	   GOSUB LOAD.DATA
	 END
	 SNAME='' ; LNAME='' ; PROD.NAME='' ; ARR.ID='' ; R.ARR=''
	REPEAT
RETURN

PROCESS.PAY.TYPE:

    PROP.LIST = DUE.PROPS<DCNT,PAY.CNT>
    PROP.LIST = RAISE(PROP.LIST)
    CALL AA.GET.PROPERTY.CLASS(PROP.LIST,PROP.CLS.LIST)
    TOT.PROP = DCOUNT(PROP.LIST,VM)
    FOR PROP.CNT = 1 TO TOT.PROP
        PROP.AMT = DUE.PROP.AMTS<DCNT,PAY.CNT,PROP.CNT>

        BEGIN CASE
        CASE PROP.CLS.LIST<1,PROP.CNT> EQ 'ACCOUNT'         ;*Add to Principal
            TOT.PRIN.PAYM += PROP.AMT

        CASE PROP.CLS.LIST<1,PROP.CNT> EQ 'INTEREST'        ;*Add to Interest
            TOT.INT.PAYM += PROP.AMT

        CASE PROP.CLS.LIST<1,PROP.CNT> EQ 'CHARGE'          ;*Add to Charge
            TOT.CHG.PAYM += PROP.AMT

        END CASE

        IF DUE.METHODS<DCNT,PAY.CNT, PROP.CNT> MATCHES 'DUE':VM:'INFO' THEN
            TOT.DUE.PAYM += PROP.AMT
        END ELSE
            TOT.CAP.PAYM += PROP.AMT
        END
    NEXT PROP.CNT
    RETURN
LOAD.DATA:
     PY.LIST=TRIM(PY.LIST,',','A')
	 PY.LIST=TRIM(PY.LIST,"'",'A')
	 PY.LIST=TRIM(PY.LIST,"~",'A')
     CHANGE FM TO ',' IN PY.LIST
     REQUEST=PY.LIST:"~SCH"
     CALLJ "com.fdh.loader.Loader", "loader", REQUEST SETTING V.RESPONSE ON ERROR PRINT 'recieved from JAVA' : V.RESPONSE :',rtncd=':SYSTEM(0)
     PY.LIST=''
RETURN
END