    SUBROUTINE JSL.TOPS.XTR.ACCOUNTOVERDRAWN
**********************************************************************************
* Desc: The routine gets OD Accounts and Calculates the aging                           
* Initial Author: Fishani Chirwa  on 1 July 2014                                        
* Title: BI Account Overdraft Data Extraction
* Adapted By: Yisau Ramon ; For Tops Analytics
* Date: 1st May 2018
**********************************************************************************

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.ACCOUNT
    $INSERT I_F.DATES
    $INSERT I_F.ACCOUNT.OVERDRAWN
    $INSERT I_F.LIMIT
    $INSERT I_F.INTERCO.PARAMETER
	$INSERT I_F.CATEGORY
	$INSERT I_F.DEPT.ACCT.OFFICER
	$INSERT I_F.COMPANY
	
	GOSUB INIT
	GOSUB PROCESS
	RETURN
	
INIT:

    FN.INTERCO='F.INTERCO.PARAMETER'
    F.INTERCO=''
    CALL OPF(FN.INTERCO,F.INTERCO)
	
	FN.LIM='F.LIMIT'
	F.LIM=''
	CALL OPF(FN.LIM,F.LIM)
	
    REPORT.DATE=R.DATES(EB.DAT.LAST.WORKING.DAY)[1,4]:'-':R.DATES(EB.DAT.LAST.WORKING.DAY)[5,2]:'-':R.DATES(EB.DAT.LAST.WORKING.DAY)[7,2]
    DB.DATE=TODAY[1,4]:'-':TODAY[5,2]:'-':TODAY[7,2] ; API.ID='JSL.TOPS.API' ; V.RESPONSE='' ; RET.ERR=''
    DLM='|' ; DTE='' ; ST='' ; ST.NAME='' ; OUT.ARR='' ; TODAY.DAYS='' ; R.LIM='' ; OD.LIST='' ; PY.LIST=''
    V.DATE=TODAY ; API.ID='JSL.TOPS.API' ; DATE.FIRST='' ; DATE.LAST='' ; DATE.DIFF='' ; R.OD='' ; OD.ID='' ; EXP.DATE=''
	
	SYS.ID='SYSTEM'
	READ R.INTERCO FROM F.INTERCO,SYS.ID ELSE NULL
	MNE.LIST=R.INTERCO<ST.ICP.FIN.MNEMONIC>
    RETURN

PROCESS:

    MNE.CNT=DCOUNT(MNE.LIST,@VM)
    FOR J=1 TO MNE.CNT
    MNE=MNE.LIST<1,J>
	FN.OD = 'F':MNE:'.ACCOUNT.OVERDRAWN'
	F.OD = ''
    CALL OPF(FN.OD,F.OD)

    FN.AC = 'F':MNE:'.ACCOUNT'
	F.AC = ''
    CALL OPF(FN.AC,F.AC)

	
    OD.CMD = 'SELECT ':FN.OD:' WITH CLRD.BAL.LIMIT LIKE "-..."'
    CALL EB.READLIST(OD.CMD,OD.LIST,'','','')
    LOOP
        REMOVE OD.ID FROM OD.LIST SETTING OD.POS
    WHILE OD.POS:OD.ID
        CALL F.READ(FN.OD,OD.ID,R.OD,F.OD,OD.ERR)
		CALL F.READ(FN.AC,OD.ID,R.AC,F.AC,AC.ERR)
        IF R.AC<AC.CATEGORY>[1,1] = 3 THEN CONTINUE
        CHECK.DAYS = ICONV(V.DATE,'D')
		IF R.AC<AC.LIMIT.REF> THEN
		ZERO.CNT=10-LEN(R.AC<AC.LIMIT.REF>)
		LIM.RF=R.OD<AC.OD.CUSTOMER>:".":STR("0",ZERO.CNT):R.AC<AC.LIMIT.REF>
		CALL F.READ(FN.LIM,LIM.RF,R.LIM,F.LIM,LIM.ERR)
		DATE.FIRST = R.LIM<LI.DATE.1ST.EXCESS>
        DATE.LAST = R.LIM<LI.DATE.LAST.MOVED>
		DTE=R.LIM<LI.DATE.OF.EXCESS>
		TODAY.DAYS = ICONV(DTE,'D')
		END ELSE
		DATE.FIRST = R.OD<AC.OD.DATE.FIRST.OD>
        DATE.LAST = R.OD<AC.OD.DATE.LAST.MOVE>
		DTE=R.OD<AC.OD.DATE.FIRST.OD>
		TODAY.DAYS = ICONV(DTE,'D')
		END
		DATE.DIFF=CHECK.DAYS-TODAY.DAYS

        BEGIN CASE
		CASE DATE.DIFF GE 1 AND DATE.DIFF LE 30
		 ST='0-30'
		 ST.NAME='STANDARD'
		CASE DATE.DIFF GE 31 AND DATE.DIFF LE 90
		 ST='31-90'
		 ST.NAME='WATCHLIST'
		CASE DATE.DIFF GE 91 AND DATE.DIFF LE 180
		 ST='91-180'
		 ST.NAME='SUBSTANDARD'
		CASE DATE.DIFF GE 181 AND DATE.DIFF LE 360
		 ST='181-360'
		 ST.NAME='DOUBTFUL'
		CASE DATE.DIFF GT 360
         ST='>360'
		 ST.NAME='LOSS'
		END CASE
		IF R.LIM<LI.EXPIRY.DATE> THEN
		EXP.DATE=R.LIM<LI.EXPIRY.DATE>[1,4]:'-':R.LIM<LI.EXPIRY.DATE>[5,2]:'-':R.LIM<LI.EXPIRY.DATE>[7,2]
		END ELSE
		EXP.DATE=''
		END
		DTE=DTE[1,4]:'-':DTE[5,2]:'-':DTE[7,2]
		CALL DBR('CATEGORY':FM:EB.CAT.DESCRIPTION,R.AC<AC.CATEGORY>,CAT.DESC)
		CALL DBR('DEPT.ACCT.OFFICER':FM:EB.DAO.NAME,R.AC<AC.ACCOUNT.OFFICER>,OFFICER.NAME)
		CALL DBR('COMPANY':FM:EB.COM.COMPANY.NAME,R.AC<AC.CO.CODE>,BRANCH.NAME)
        OUT.ARR=OD.ID:DLM:R.OD<AC.OD.CUSTOMER>:DLM:R.AC<AC.CATEGORY>:DLM:CAT.DESC:DLM:R.AC<AC.CURRENCY>:DLM:R.OD<AC.OD.ACCOUNT.OFFICER>:DLM:OFFICER.NAME:DLM:ST:DLM:ST.NAME:DLM:EXP.DATE:DLM:R.OD<AC.OD.CLRD.BAL.LIMIT>
		OUT.ARR:=DLM:R.LIM<LI.INTERNAL.AMOUNT>:DLM:DTE:DLM:DATE.FIRST:DLM:DATE.LAST:DLM:R.OD<AC.OD.OD.EXCESS.NARR>:DLM:R.AC<AC.CO.CODE>:DLM:BRANCH.NAME:DLM:REPORT.DATE:DLM:DB.DATE
        PY.LIST<-1>=OUT.ARR
		OUT.ARR='' ; DATE.FIRST='' ; DATE.LAST='' ; R.OD='' ; R.LIM='' ; OD.ID='' ; DTE='' ; ST='' ; ST.NAME='' ; DATE.DIFF='' ; TODAY.DAYS=''
		EXP.DATE='' ; DTE='' ; R.AC=''
    REPEAT
	NEXT J
     IF PY.LIST NE '' THEN
	 PY.LIST=TRIM(PY.LIST,',','A')
	 PY.LIST=TRIM(PY.LIST,"'",'A')
	 PY.LIST=TRIM(PY.LIST,"~",'A')
	 CHANGE FM TO ',' IN PY.LIST
     REQUEST=PY.LIST:"~OD"
	 CALL EB.CALL.JAVA.API(API.ID,REQUEST,RESPONSE,RET.ERR)
     PY.LIST=''
	 END
    RETURN

END